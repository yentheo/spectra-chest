plugins {
    id 'java'
    id 'jacoco'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

archivesBaseName = 'better-chests'
version "${project.version}_${project.minecraft_version}"

repositories {
	mavenCentral()
	maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.6.1'
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'

	testImplementation group: 'org.spigotmc', name: 'spigot-api', version: "${project.minecraft_version}-${project.spigot_snapshot}"
	compileOnly group: 'org.spigotmc', name: 'spigot-api', version: "${project.minecraft_version}-${project.spigot_snapshot}"

    implementation group: 'com.google.inject', name: 'guice', version: '5.1.0'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.0.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.0.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
}

test {
	useJUnitPlatform()
    finalizedBy jacocoTestReport 
}

jacocoTestReport {
	reports {
        xml.required = true
	}
}

processResources {
	doFirst {
		file("$buildDir\\resources\\main").listFiles().each {
			it.delete()
		}
	}
}

shadowJar {
    archiveClassifier = null
    dependencies {
        include dependency('com.google.inject:guice:5.1.0')
        include dependency('com.google.guava:guava:30.1-jre')
        include dependency('aopalliance:aopalliance:1.0')
        include dependency('com.fasterxml.jackson.core:jackson-core:2.0.1')
        include dependency('com.fasterxml.jackson.core:jackson-annotations:2.0.1')
        include dependency('com.fasterxml.jackson.core:jackson-databind:2.0.1')
    }
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}

// to run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification
